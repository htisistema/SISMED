MEMVAR nivel_acess,getlist,mdata_sis
*********************************************************
**** CON4203.PRG: Relatorio -> Duplicatas PAGAS DATA OPERACAO (conta a receber)
*********************************************************

FUNCTION con4203
***************
LOCAL mprg:='CON4203',;
      t_ini,t_fim,mod,td_mes,tp_mes,pag,mcod_vend,;
      mfornec,tot,tot_g,tot_din,tot_gin,l_mes,tt,;
      xxx,mtit,mtipo,mtip,mdia,mt_dia,opcao,mquantd,mtipo_con,;
      mtp_rel:=' ',mimp_com:='N',mtot_comi:=0,i,mtot_juros:=0,mtot_ind:=0,;
      mvlr_ind:=0,msub_ind:=0,mjuros_ind:=0,mcod_aux,mmulta,mjuros,mdias_atras,;
      mcartao:=0,mordenar:=0
local nErr, nPos,;
        vEmp := {},aret:={},;
        nCnn,oSql,;
        cComm, apCode,m_param:={}

PRIVATE mcliente,mcgc,mcpf,mcod_cli,bank,mnome_vend,mcom_ven,;
        mtipo_imp,mimp_tipo:=0,marq:=SPACE(35),;
        marq_prin,mimp_rel,mporta_imp,mcodemp:=SPACE(3),mcod_oper:=0

IF ! ver_nivel(mprg,'CONTA A RECEBER (RELATORIO DE DOCUMENTOS PAGOS P/DATA OPERACAO)','156',nivel_acess,,'AMBIE')
        RETURN NIL
ENDIF
*---------------------------------------------------------------------
CLOSE ALL
IF ! AbriArq('insopera','sen');RETURN NIL;ENDIF
IF ! AbriArq('sacsetup','set');RETURN NIL;ENDIF
IF ! AbriArq('saccli','cli');RETURN NIL;ENDIF
IF ! AbriArq('sacped_s','ped_s');RETURN NIL;ENDIF
IF ! AbriArq('sacdupr','dupr');RETURN NIL;ENDIF
*---------------------------------------------------------------------
****************
SELE('cli');ORDSETFOCUS(1)
****************
op_tela(10,05,22,77," Documentos PAGOS p/Data Operacao ")
@ 00,00 SAY "Codigo da Empresa........:"
@ 01,00 SAY "Tipo de Documento........:"
@ 02,00 SAY "Codigo do Cliente........:"
@ 03,00 SAY "Codigo do Vendedor.......:"
@ 04,00 SAY "Codigo do Operador.......:"
@ 05,00 SAY "Pag.em [C]arteira [B]anco:"
@ 06,00 SAY "Data do Inicio...........:"
@ 07,00 SAY "Data do Final............:"
@ 09,00 SAY "So Docum. Transferido [S/n]...:"
@ 10,00 SAY "Imprimir as Comissao[S/n].....:"
WHILE .T.
********* VARIAVEIS DE IMPRESSAO *******************
        mtipo_imp := 'J'
****************************************************
        bank=SPACE(1)
        mcod_cli := mcod_vend := mod := 0
        mcliente := SPACE(30)
        t_ini := t_fim := CTOD("  /  /  ")
        mfornec := SPACE(4)
        mtp_rel := 'N'
        mtip := SPACE(2)
        mimp_com := 'N'
        @ 01,27 GET mtip PICT "@!" VALID ver_tipdc(mtip,'*')
        @ 02,27 GET mcod_cli pict "99999" VALID ver_cli(mcod_cli,02,33) WHEN mcod_vend = 0 
        READ
        IF ! EMPTY(mcod_cli);ver_cli(mcod_cli,02,33);ENDIF
        @ 03,27 GET mcod_vend pict "999" VALID ven(mcod_vend,03,31) WHEN mcod_cli = 0 
        @ 04,27 GET mcod_oper pict "999" VALID ven(mcod_oper,04,31)
        @ 05,27 GET bank pict "@!" VALID bank $ 'C,B, '
        @ 06,27 GET t_ini 
        @ 07,27 GET t_fim 
        @ 09,34 GET mtp_rel PICT '@!' VALID mtp_rel $ 'S,N'
        @ 10,34 GET mimp_com PICT '@!' VALID mimp_com $ 'S,N'
        READ
        IF LASTKEY()=27
                EXIT
        ENDIF
        IF mtip = 'DU'
                mtit := "DUPLICATAS PAGAS (Contas a Receber)"
                mtip := 'DU'
        ELSEIF mtip = 'CH'
                mtit := "CHEQUES PAGOS (Contas a Receber)"
                mtip := 'CH'
        ELSEIF mtip = 'CT'
                mtit := "CARTOES PAGOS (Contas a Receber)"
                mtip := 'CT'
                mcartao := 0
                op_tela(19,10,21,75,' Codigo do Cartao ')
                @ 00,00 SAY "Codigo do Cartao.........:"
                @ 00,27 GET mcartao PICT '999' VALID ver_cartao(mcartao,00,30)
                READ
                IF LASTKEY() = 27
                        wvw_lclosewindow()
                        LOOP
                ENDIF
                opcao := op_simnao('S','Confirma o Cartao:')
                IF opcao = 'N'
                        wvw_lclosewindow()
                        LOOP
                ENDIF
                wvw_lclosewindow()
        ELSEIF mtip = 'FI'
                mtit := "FINANCEIRA PAGOS (Contas a Receber)"
                mtip := 'FI'
                mcartao := 0
                op_tela(19,10,21,75,' Codigo da Financeira ')
                @ 00,00 SAY "Codigo Financeira........:"
                @ 00,27 GET mcartao PICT '999' VALID ver_finan(mcartao,00,30)
                READ
                IF LASTKEY() = 27
                        wvw_lclosewindow()
                        LOOP
                ENDIF
                opcao := op_simnao('S','Confirma a Financeira:')
                IF opcao = 'N'
                        wvw_lclosewindow()
                        LOOP
                ENDIF
                wvw_lclosewindow()
        ELSE
                mtit := "DOCUMENTOS PAGOS (Contas a Receber)"
        ENDIF
        op_tela(10,10,13,50,'FORMAS DE ORDENAR O RELATORIO')
        @ 01,00 PROMPT ' Data de Operacao + Fornecedor         '
        @ 02,00 PROMPT ' Data de Operacao + Banco + Fornecedor '
        MENU TO mordenar
        wvw_lclosewindow()
        mtit := mtit
        i := 0
        td_mes := tp_mes := 0    && tot. pagar no mes
        IF t_ini = CTOD("  /  /  ") .OR. t_fim = CTOD('  /  /  ')
                t_ini=CTOD("01/01/94")
                t_fim=CTOD("31/12/10")
        ENDIF

        mensagem('Aguarde o Coletando dados p/ impressao...')
        ************************************************************
        cComm  := "SELECT * FROM sacdupr WHERE datope >= "+sr_cdbvalue(t_ini)+" AND datope <= "+sr_cdbvalue(t_fim)+" AND pago IS NOT NULL AND datpag IS NOT NULL"
        IF ! EMPTY(mcod_cli)
                ccomm := ccomm + " AND fornec = "+sr_cdbvalue(STRZERO(mcod_cli,5))
        ENDIF
        IF ! EMPTY(mtip)
                ccomm := ccomm + " AND tipo = "+sr_cdbvalue(mtip)
        ENDIF
        IF ! EMPTY(mcartao)
                ccomm := ccomm + " AND numero = "+sr_cdbvalue(mcartao)
        ENDIF
        IF ! EMPTY(mcod_vend)
                ccomm := ccomm + " AND vendedor = "+sr_cdbvalue(STRZERO(mcod_vend,3))
        ENDIF
        IF ! EMPTY(mcod_oper)
                ccomm := ccomm + " AND operador = "+sr_cdbvalue(STRZERO(mcod_oper,3))
        ENDIF
        IF ! EMPTY(bank)
                ccomm := ccomm + " AND banco = "+sr_cdbvalue(bank)
        ENDIF
        IF mtp_rel = 'S'
                ccomm := ccomm + " AND doc_tran IS NUL"
        ENDIF
        IF mordenar = 1
                ccomm := ccomm + " ORDER BY datope,fornec,duplicata"
        ELSE
                ccomm := ccomm + " ORDER BY datope,numero,fornec,duplicata"
        ENDIF
        apCode := SR_SQLParse( cComm, @nErr, @nPos )
        cComm  := SR_SQLCodeGen(apCode,{t_ini,t_fim},sr_getconnection():nsystemid)
        //atencao(ccomm)
        dbUseArea( .F.,"SQLRDD",cComm,"q_dupr")
        malias := 'q_dupr'
        SELE('q_dupr')
        //atencao(STRZERO(LASTREC(),8))
        DBGOTOP()
        IF EOF()
                atencao('Nao existe documento neste Periodo...')
                (malias)->(DBCLOSEAREA())
                LOOP
        ENDIF

        IF ! imp_arq('DOC_EMIT.REL','R')
                (malias)->(DBCLOSEAREA())
                LOOP
        ENDIF
        SET PRINT ON
        pag:= tot := tot_g := tot_din := tot_gin := mt_dia := 0
        mfornec := q_dupr->fornec
        l_mes=SUBSTR(DTOC(q_dupr->datope),4,5)
        mdia := q_dupr->datope
        mtipo := DTOC(t_ini)+' a '+DTOC(t_fim)
        IF ! EMPTY(mcod_oper)
                mtipo := mtipo + ' - Cod.Operador:'+STRZERO(mcod_oper,3)
        ENDIF
        //WHILE q_dupr->datope <= t_fim .AND. ! EOF()
        WHILE ! EOF()
                mcliente := SPACE(30)
                IF pag=0 .OR. PROW()>=59
                        pag=pag+1
                        IF pag>1
                                EJECT
                        ENDIF
                        cabecalho(pag,mtit,mtipo)
                        imprt(mtipo_imp,'C')
                        IF mcod_vend > 0
                                DEVPOS(PROW()+1,00);DEVOUT('Vendedor: '+STRZERO(mcod_vend,3)+' - '+mnome_vend)
                                DEVPOS(PROW(),00);DEVOUT('          '+STRZERO(mcod_vend,3)+' - '+mnome_vend)
                        ENDIF
                        ? "Cod.  Cliente                                  No. Documento      Tp.Pag.  Emissao  Vencim. Data.PG       Valor  Dias  Sit.  Data Op"
                        IF mimp_com = 'S'
                                ?? ' % Com  Vlr.Com No.Ped.'
                        ENDIF
                        ? REPLICATE("-",137)
                        ?
                ENDIF
                IF mdia <> q_dupr->datope
                        IF ! EMPTY(mt_dia)
                                DEVPOS(PROW()+1,86);DEVOUT('           --------------')
                                DEVPOS(PROW()+1,71);DEVOUT('Total da Emissao '+DTOC(mdia)+':')
                                DEVPOS(PROW(),PCOL()+1);DEVOUT(TRANSFORM(mt_dia,'99,999,999.99'))
                                ? REPLICATE("*",137)
                        ENDIF
                        mt_dia := 0
                        mdia := q_dupr->datope
                ENDIF
                ? q_dupr->fornec+" "
                ?? q_dupr->cliente+" "
                ?? q_dupr->numero+" "
                ?? q_dupr->tipo+q_dupr->duplicata+q_dupr->conta+" "
                IF q_dupr->banco = 'C'
                        ?? 'CARTEIRA'+" "
                ELSEIF q_dupr->banco = 'B'
                        ?? 'BANCO   '+" "
                ELSE
                        ?? '        '+" "
                ENDIF
                ?? DTOC(q_dupr->emissao)+" "
                ?? DTOC(q_dupr->venc)+" "
                ?? DTOC(q_dupr->datpag)+" "
                ?? transform(q_dupr->vlpago,"@E 999,999.99")+" "
                ?? transform(q_dupr->datpag-mdata_sis,"9999")
                ?? ' Pago  '
                ?? '  '+DTOC(q_dupr->datope)
                IF mimp_com = 'S'
                        ?? ' '+TRANSFORM(q_dupr->comissao*100,'99.99')
                        ?? ' '+TRANSFORM(q_dupr->vlpago*q_dupr->comissao,'9,999.99')
                        ?? ' '+q_dupr->num_ped
                        mtot_comi := mtot_comi + (q_dupr->vlpago*q_dupr->comissao)
                ENDIF
                tot=tot+1
                tot_g=tot_g+1
                tot_din=tot_din+q_dupr->vlpago
                tot_gin=tot_gin+q_dupr->vlpago
                mt_dia := mt_dia + q_dupr->vlpago
                SKIP
        ENDDO
        IF mdia <> q_dupr->datope
                IF ! EMPTY(mt_dia)
                        DEVPOS(PROW()+1,86);DEVOUT('              --------------')
                        DEVPOS(PROW()+1,71);DEVOUT('Total da Emissao '+DTOC(mdia)+':')
                        DEVPOS(PROW(),PCOL()+1);DEVOUT(TRANSFORM(mt_dia,'99,999,999.99'))
                        ? REPLICATE("*",137)
                ENDIF
        ENDIF
        IF PROW() >= 57
                EJECT
                pag=pag+1
                cabecalho(pag,mtit,mtipo)
        ENDIF
        ?
        imprt(mtipo_imp,'N')
        ? "*** RESUMO FINAL ***"
        ?
        ? "Total de Documentos EMITIDOS no Periodo: "+transform(tot_g,"9999")+" - "+ltrim(transform(tot_gin,"@E 999,999,999,999.99"))
        IF mimp_com = 'S'
                ? "Total de Comissao: "+ltrim(transform(mtot_comi,"@E 999,999.99"))
        ENDIF
        ?
        ? time()
        EJECT
        SETPRC(00,00)
        SET DEVI TO SCREEN;SET PRINT TO;SET PRINT OFF
        (malias)->(DBCLOSEAREA())
        conf_impressao('DOC_EMIT.REL')
        /*
        IF mimp_tipo = 2
                lertexto('DOC_EMIT.REL')
        ELSEIF mtipo_imp = 'D'
                
        ELSE
                MYRUN('DOSPRINTER /RAW /DEL '+ALLTRIM(m_indiv[1,5])+'HTI.REL')
        ENDIF
        */
ENDDO
wvw_lclosewindow()
RETURN NIL
******************************* F I M *******************************



